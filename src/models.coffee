_ = require 'lodash'

class Datacenter
    constructor: (actanoJson, @defaults) ->
        @lans = []
        @servers = []

        @json =
            properties:
                name: actanoJson.name
                description: actanoJson.description ? @defaults.datacenter.description
                location: actanoJson.location ? @defaults.datacenter.location

        @_addLans actanoJson.lans
        @_addServers actanoJson.servers

    getName: ->
        return @json.properties.name

    getLans: ->
        return @lans

    getServers: ->
        return @servers

    toJson: ->
        return @json

    _addLans: (lans = []) ->
        for lanJson in lans
            lan = new Lan lanJson, @defaults
            @_addLan lan

    _addServers: (servers = []) ->
        for serverJson in servers
            server = new Server serverJson, @defaults
            @_addServer server

    _addLan: (lan) ->
        @lans.push lan

    _addServer: (server) ->
        @servers.push server


class Lan
    constructor: (actanoJson, defaults) ->
        @json =
            properties:
                name: actanoJson.name
                public: actanoJson.public ? defaults.lan.public

    getName: ->
        return @json.properties.name

    toJson: ->
        return @json


class Server
    constructor: (actanoJson, @defaults) ->
        @nics = []
        @volumes = []

        @json =
            properties:
                name: actanoJson.name
                cores: actanoJson.cores ? @defaults.server.cores
                ram: actanoJson.ram ? @defaults.server.ram
                availabilityZone: actanoJson.availabilityZone ? @defaults.server.availabilityZone
                bootVolume: id: '<auto-generated-id>'
            entities:
                nics:
                    items: []
                volumes:
                    items: []

        @_addVolumes actanoJson.volumes
        @_addNics actanoJson.nics

    getName: ->
        return @json.properties.name

    getNics: ->
        return @nics

    getVolumes: ->
        return @volumes

    setBootVolumeId: (id) ->
        @json.properties.bootVolume.id = id

    _addVolumes: (volumes) ->
        for volumeJson in volumes
            unless volumeJson.name?
                @_setVolumeName volumeJson

            volume = new Volume volumeJson, @defaults
            @_addVolume volume

    _addNics: (nics) ->
        for nicJson in nics
            nic = new Nic nicJson, @defaults
            @_addNic nic

    _addVolume: (volume) ->
        @volumes.push volume

    _addNic: (nic) ->
        @nics.push nic

    _setVolumeName: (volumeJson) ->
        postfix = volumeJson.namePostfix ? @defaults.volume.namePostfix
        volumeJson.name = @getName() + postfix

    toJson: ->
        result = _.cloneDeep @json

        for nic in @nics
            result.entities.nics.items.push nic.toJson()

        return result


class Volume
    constructor: (actanoJson, defaults) ->
        @imageName = actanoJson.image ? defaults.volume.image

        @json =
            properties:
                name: actanoJson.name
                type: actanoJson.type ? defaults.volume.type
                size: actanoJson.size ? defaults.volume.size
                image: '<auto-generated-id>'
                bus: actanoJson.bus ? defaults.volume.bus

    getName: ->
        return @json.properties.name

    getImageName: ->
        return @imageName

    setImageId: (id) ->
        @json.properties.image = id

    toJson: ->
        return @json


class Nic
    constructor: (actanoJson, defaults = {}) ->
        @json =
            properties:
                name: actanoJson.name
                ips: actanoJson.ips ? defaults.nic.ips
                dhcp: actanoJson.dhcp ? defaults.nic.dhcp
                lan: '<auto-generated-id>'
                firewallActive: actanoJson.firewallActive ? defaults.nic.firewallActive

    getName: ->
        return @json.properties.name

    setLanId: (id) ->
        @json.properties.lan = id

    toJson: ->
        return @json


module.exports = {
    Datacenter
    Lan
    Server
    Volume
    Nic
}
